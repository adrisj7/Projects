// 2019-05-23 21:24:50
#define __tilemap
/// Tile map!
    // All scripts related to tile maps

    // Tilemap struct
    enum Tilemap {
        map,
        twidth,
        theight,
        sizeof
    }


#define __get_tilemap_grid
/// __get_tilemap_grid(tilemap)
    var tilemap:Tilemap = argument0;

    return tilemap.map;


#define __get_tilemap_collision
/// __get_tilemap_collision()
    var g = __get_gamestate();
    return g._tilemap_collision;


#define __get_tilemap_event
/// __get_tilemap_event()
    var g = __get_gamestate();
    return g._tilemap_event;


#define __get_tilemap_character
/// __get_tilemap_character()
    var g = __get_gamestate();
    return g._tilemap_character;


#define __refill_collision_tilemap
/// __refill_collision_tilemap()
    // Refills the collision tilemap with all collideable objects and tiles
    // in the room

    var c = __get_tilemap_collision();

    // Step 1: Clear all collisions
    tilemap_clear(c, false);

    // Collider objects must inherit objYUMEngineCollider
    with (objYUMEngineCollider) {
        tilemap_collision_set_with_object(id, true);
    }

    // TODO: Tileset collideables


#define __refill_event_tilemap
/// __refill_event_tilemap()

    // TODO: Events


#define __refill_character_tilemap
/// __refill_character_tilemap()

    // TODO: Characters


////////////////////////////////////////////////////////////////////////////////


#define tilemap_create
/// tilemap_create(width_in_tiles, height_in_tiles, twidth, theight)
    #args width_in_tiles, height_in_tiles, twidth, theight

    // Our new tilemap
    var result:Tilemap = Tilemap();
    result.map     = ds_grid_create(width_in_tiles, height_in_tiles);
    result.twidth  = twidth;
    result.theight = theight;

    return result;


#define tilemap_create_and_fill_room
/// tilemap_create_and_fill_room(twidth, theight)
    // creates a new tilemap that sets its width and height (in tiles) to fill the 
    // current room
    #args twidth, theight

    var tilemap_width  = ceil(room_width  / twidth),
        tilemap_height = ceil(room_height / theight);

    return tilemap_create(tilemap_width, tilemap_height, twidth, theight);


#define tilemap_clear
/// tilemap_clear(tilemap, val)
    // clears a tilemap to a specific value. Akin to ds_grid_clear
    #args tilemap, val

    var grid = __get_tilemap_grid(tilemap);
    ds_grid_clear(grid, val);


#define tilemap_get_tile
/// tilemap_get_tile(tilemap, tile_xpos, tile_ypos)
    // returns the value of a tile stored in this tilemap. If it's an invalid tile 
    // it returns -1
    #args tilemap, tile_xpos, tile_ypos

    var grid = __get_tilemap_grid(tilemap);

    return grid[# tile_xpos, tile_ypos];


#define tilemap_set_tile
/// tilemap_set_tile(tilemap, tile_xpos, tile_ypos, value)
    #args tilemap, tile_xpos, tile_ypos, value

    var grid = __get_tilemap_grid(tilemap);

    grid[# tile_xpos, tile_ypos] = value;


#define tilemap_resize
/// tilemap_resize(tilemap, width_in_tiles, height_in_tiles)
    #args tilemap, width_in_tiles, height_in_tiles

    var grid = __get_tilemap_grid(tilemap);

    ds_grid_resize(grid, width_in_tiles, height_in_tiles);


#define tilemap_resize_to_fill_room
/// tilemap_resize_to_fill_room(tilemap)
    // resizes a tilemap's width and height (in tiles) to fill the current room
    #args tilemap

    var twidth =  tilemap_get_tile_width(tilemap),
        theight = tilemap_get_tile_height(tilemap);

    var tilemap_width  = ceil(room_width  / twidth),
        tilemap_height = ceil(room_height / theight);

    tilemap_resize(tilemap, tilemap_width, tilemap_height);


#define tilemap_meeting
/// tilemap_meeting(xa, ya, tilemap)
    // effectively place_meeting but for tilemaps. (in theory, both can be used 
    // interchangeably for collisions)
    var dx      = argument0 - x,
        dy      = argument1 - y,
        tmap_id = argument2;

    var tleft   = tilemap_get_tile_x(tmap_id, bbox_left + dx),
        tright  = tilemap_get_tile_x(tmap_id, bbox_right + dx),
        ttop    = tilemap_get_tile_y(tmap_id, bbox_top + dy),
        tbot    = tilemap_get_tile_y(tmap_id, bbox_bottom + dy);

    // Go through all tiles within the bounding box
    for (var tx = tleft; tx <= tright; ++tx) {
        for (var ty = ttop; ty <= tbot; ++ty) {
            if tilemap_get_tile(tmap_id, tx, ty)
                return true;
        }
    }

    return false;


#define tilemap_get_pos_x
/// tilemap_get_pos_x(tilemap, tile_xpos)
    // gets the in-game x pixel coordinate of a tile
    #args tilemap, tile_xpos

    // Simply scale by the tile width
    var twidth = tilemap_get_tile_width(tilemap);
    return tile_xpos * twidth;


#define tilemap_get_pos_y
/// tilemap_get_pos_y(tilemap, tile_ypos)
    // gets the in-game y pixel coordinate of a tile
    #args tilemap, tile_ypos

    // Simply scale by the tile height
    var theight = tilemap_get_tile_height(tilemap);
    return tile_ypos * theight;


#define tilemap_get_tile_x
/// tilemap_get_tile_x(tilemap, pos_x)
    // gets the tile coordinate of an in-game x pixel coordinate
    #args tilemap, pos_x

    // Simply scale down by the tile width
    var twidth = tilemap_get_tile_width(tilemap);
    return floor(pos_x / twidth);


#define tilemap_get_tile_y
/// tilemap_get_tile_y(tilemap, pos_y)
    // gets the tile coordinate of an in-game y pixel coordinate
    #args tilemap, pos_y

    // Simply scale down by the tile height
    var theight = tilemap_get_tile_height(tilemap);
    return floor(pos_y / theight);


#define tilemap_get_width_in_tiles
/// tilemap_get_width_in_tiles(tilemap);
    // returns the tilemap's width (in tiles)
    #args tilemap

    var grid = __get_tilemap_grid(tilemap);

    return ds_grid_width(grid);


#define tilemap_get_height_in_tiles
/// tilemap_get_height_in_tiles(tilemap);
    // returns the tilemap's height (in tiles)
    #args tilemap

    var grid = __get_tilemap_grid(tilemap);

    return ds_grid_height(grid);


#define tilemap_get_tile_width
/// tilemap_get_tile_width(tilemap)
    // returns the width of a tilemap's tiles (in pixels)
    var tilemap:Tilemap = argument0;

    return tilemap.twidth;


#define tilemap_get_tile_height
/// tilemap_get_tile_height(tilemap)
    // returns the width of a tilemap's tiles (in pixels)
    var tilemap:Tilemap = argument0;

    return tilemap.theight;


#define tilemap_global_set_tile_size
/// tilemap_global_set_tile_size(twidth, theight)
    // sets the tile size of all background tilemaps. 
    // This will cause all tilemaps to reload and re-fill themselves, so this 
    // should in theory only be done once at the beginning of the game
    #args twidth, theight

    // Set gamestate properties
    var g = __get_gamestate();
    g._tile_width = twidth;
    g._tile_height = theight;

    var col:Tilemap = __get_tilemap_collision(),
        evt:Tilemap = __get_tilemap_event(),
        cha:Tilemap = __get_tilemap_character();


    // Manually change the tilemap tile size
    col.twidth = twidth;
    col.theight = theight;
    evt.twidth = twidth;
    evt.theight = theight;
    cha.twidth = twidth;
    cha.theight = theight;

    // Resize the tilemaps
    tilemap_resize_to_fill_room(col);
    tilemap_resize_to_fill_room(evt);
    tilemap_resize_to_fill_room(cha);


    // Refill them
    __refill_collision_tilemap();
    __refill_event_tilemap();
    __refill_character_tilemap();


#define tilemap_global_get_tile_x
/// tilemap_global_get_tile_x(pos_x)
    // gets the tile coordinate of an in-game x pixel coordinate
    #args pos_x
    return tilemap_get_tile_x(__get_tilemap_collision(), pos_x);


#define tilemap_global_get_tile_y
/// tilemap_global_get_tile_y(pos_y)
    // gets the tile coordinate of an in-game y pixel coordinate
    #args pos_y
    return tilemap_get_tile_y(__get_tilemap_collision(), pos_y);


#define tilemap_global_get_pos_x
/// tilemap_global_get_pos_x(tile_xpos)
    // gets the in-game x pixel coordinate of a tile
    #args tile_xpos
    return tilemap_get_pos_x(__get_tilemap_collision(), tile_xpos);


#define tilemap_global_get_pos_y
/// tilemap_global_get_pos_y(tile_ypos)
    // gets the in-game y pixel coordinate of a tile
    #args tile_ypos
    return tilemap_get_pos_y(__get_tilemap_collision(), tile_ypos);


#define tilemap_global_get_tile_width
/// tilemap_global_get_tile_width()
    var g = __get_gamestate();
    return g._tile_width;


#define tilemap_global_get_tile_height
/// tilemap_global_get_tile_height()
    var g = __get_gamestate();
    return g._tile_height;


#define tilemap_collision_set
/// tilemap_collision_set(tile_xpos, tile_ypos, collide)
    #args tile_xpos, tile_ypos, collide

    var c = __get_tilemap_collision();
    tilemap_set_tile(c, tile_xpos, tile_ypos, collide);


#define tilemap_collision_get
/// tilemap_collision_get(tile_xpos, tile_ypos)
    #args tile_xpos, tile_ypos

    var c = __get_tilemap_collision();
    return tilemap_get_tile(c, tile_xpos, tile_ypos);


#define tilemap_collision_set_with_object
/// tilemap_collision_set_with_object(ind, value)
    // sets the collision value of all tiles that overlap an object's mask
    #args ind, value

    var tleft   = tilemap_global_get_tile_x(ind.bbox_left),
        tright  = tilemap_global_get_tile_x(ind.bbox_right),
        ttop    = tilemap_global_get_tile_y(ind.bbox_top),
        tbot    = tilemap_global_get_tile_y(ind.bbox_bottom);
    for(var xx = tleft; xx <= tright; ++xx) {
        for(var yy = ttop; yy <= tbot; ++yy) {
            tilemap_collision_set(xx, yy, true);
            //show_debug_message("[DEBUG] : Set " + string(xx) + "\t,\t " + string(yy));
        }
    }


#define tilemap_collision_meeting
/// tilemap_collision_meeting(dx, dy)
    #args dx, dy


#define object_get_tile_x
/// object_get_tile_x(ind)
    #args ind

    var pos_x = ind.x;
    return tilemap_global_get_tile_x(pos_x);


#define object_get_tile_y
/// object_get_tile_y(ind)
    #args ind

    var pos_y = ind.y;
    return tilemap_global_get_tile_y(pos_y);