// 2019-06-09 14:46:08
#define __menu
/// __menu()
    // Menu system!
    enum MenuType {
        None,       // No menu is open
        Main,       // Main Menu
        QuitConfirm,// Are you sure?
        Player,     // Pressing X in game
        Inventory,  // Inventory/Effects Menu
        Save,       // Save Game
        Load,        // Load Game
        Script
    }


#define __get_menu_system
/// __get_menu_system()
    return singleton_get(__YUMEngineMenuSystem);


#define __get_menu_stack
/// __get_menu_stack()
    var sys = __get_menu_system();
    return sys._menu_stack;


#define __menu_get_system_renderer
/// __menu_get_system_renderer(type)
    #args type
    // TODO: Fill these in while you make em
    var obj_index = noone;
    switch type {
        case MenuType.Main:
            obj_index = __YUMEngineMenuMain;
            break;
        case MenuType.QuitConfirm:
            obj_index = __YUMEngineMenuQuitCheck;
            break;
        case MenuType.Player:
            obj_index = __YUMEngineMenuPlayer;
            break;
        case MenuType.Inventory:
            obj_index = __YUMEngineMenuInventory;
            break;
        case MenuType.Save:
            obj_index = noone;
            break;
        case MenuType.Load:
            obj_index = noone;
            break;
        case MenuType.Script:
            obj_index = __YUMEngineMenuScript;
            break;
    }
    if obj_index != noone {
        return singleton_get(obj_index);
    }
    return noone;


#define __menu_stack_empty
/// __menu_stack_empty()
    var stack = __get_menu_stack();
    return ds_list_empty(stack);


#define __menu_stack_pop
/// __menu_stack_pop()
    var stack = __get_menu_stack();
    // if __menu_stack_empty() {
    //     // If we're empty, release control
    //     var sys = __get_menu_system();
    //     sys._menu_open = false;
    // } else {
    ds_list_stack_pop(stack);
    // __menu_start_next();
    //}


#define __menu_stack_pop_all
/// __menu_stack_pop_all()
    while !__menu_stack_empty()
            __menu_stack_pop();
    

#define __menu_get_current_type
/// __menu_get_current_type()
    // returns the current menu type
    var sys = __get_menu_system();
    return sys._menu_current;


#define __menu_get_next_type
/// __menu_get_next_type()
    if __menu_stack_empty() {
        return MenuType.None;
    }
    var st = __get_menu_stack();
    return ds_list_stack_top(st);


#define __menu_get_current_renderer
/// __menu_get_current_renderer()
    var type = __menu_get_current_type();
    return __menu_get_system_renderer(type);


#define __menu_get_next_renderer
/// __menu_get_next_renderer()
    var type = __menu_get_next_type();
    return __menu_get_system_renderer(type);


#define __menu_start_next
/// __menu_start_next()
    // Starts the next menu
    var sys = __get_menu_system();
    __menu_start_next_ext(sys._fade_duration_default);


#define __menu_start_next_ext
/// __menu_start_next_ext(duration)
    #args duration
    // Starts the next menu, with a certain duration transition
    var sys = __get_menu_system();
    sys._fading = true;
    sys._fade_closing = true;
    sys._menu_open = true;
    sys._fade_duration = duration;
    screen_set_fade(1, duration);


#define __menu_force_close
/// _menu_force_close()
    // Clears the menu and force closes it
    var sys = __get_menu_system();
    sys._menu_open = false;


#define __menu_set_script
/// __menu_set_script(script)
    #args script
    // Sets the menu script type
    var ren = __menu_get_system_renderer(MenuType.Script);
    ren._script = script;


#define __menu_is_fading
/// __menu_is_fading()
    // Is the menu transitioning?
    var sys = __get_menu_system();
    return sys._fading;


#define __menu_sys_is_just_opened
/// __menu_sys_is_just_opened()
    // Has this menu system just been opened?
    return _active && !_last_active;


#define __menu_is_waiting_to_open
/// __menu_is_waiting_to_open()
    // Returns whether we're waiting for the menu to open
    var dsys = __get_dialogue_system();
    return dsys._menu_input_buffer;


#define menu_is_open
/// menu_is_open()
    var sys = __get_menu_system();
    return sys._menu_open;


#define menu_open
/// menu_open(type)
    #args type
    var sys = __get_menu_system();
    menu_open_ext(type, sys._fade_duration_default);


#define menu_open_ext
/// menu_open_ext(type, duration)
    // Adds a menu to our menu stack
    #args type, duration
    var st = __get_menu_stack();
    ds_list_stack_push(st, type);
    // New menu! This menu is always the next one
    __menu_start_next_ext(duration);


#define menu_close
/// menu_close()
    // Pop off the top and start the next
    __menu_stack_pop();
    __menu_start_next();


#define menu_close_ext
/// menu_close_ext(duration)
    #args duration
    // Closes our current menu
    // var st = __get_menu_stack();
    //__menu_stack_pop();
    // ds_list_stack_pop(st);
    __menu_stack_pop();
    __menu_start_next_ext(duration);


#define menu_close_all
/// menu_close_all()
    var sys = __get_menu_system();
    menu_close_all_ext(sys._fade_duration_default);


#define menu_close_all_ext
/// menu_close_all_ext(duration)
    #args duration
    // Closes all menus
    __menu_stack_pop_all();
    __menu_start_next_ext(duration);


#define main_menu_set_background
/// main_menu_set_background(background)
    // Sets the background sprite that's loaded at the beginning of the game
    #args background
    var main = __menu_get_system_renderer(MenuType.Main);
    main._background = background;